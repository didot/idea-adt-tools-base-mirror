import com.google.common.io.Files

apply plugin: 'groovy'
apply plugin: 'jacoco'
apply plugin: 'clone-artifacts'

configurations {
    provided
    includeInJar
}

// Incremental update test support
File classesDir = new File(project.buildDir, "classes/incremental-test")
File baseClasses = new File(classesDir, "base")
File baseInstrumentedClasses = new File(classesDir, "baseInstrumented")

sourceSets {
    main {
        groovy.srcDirs = ['src/main/groovy', 'src/fromGradle/groovy']
        resources.srcDirs = ['src/main/resources', 'src/fromGradle/resources']
        compileClasspath += configurations.provided
    }

    test {
        compileClasspath += files(baseClasses)
        runtimeClasspath += files(baseInstrumentedClasses)
    }
}

ext.proguardVersion = "5.2.1"

dependencies {
    compile project(':base:builder')
    compile project(':base:lint')
    compile project(':base:transform-api')

    compile 'org.ow2.asm:asm:5.0.3'
    compile 'org.ow2.asm:asm-commons:5.0.3'
    compile "net.sf.proguard:proguard-gradle:${project.ext.proguardVersion}"
    compile "org.jacoco:org.jacoco.core:0.7.4.201502262128"
    testCompile project(path:":base:fdr", configuration:'classes')


    // Add gradleApi to classpath for compilation, but use provided configuration so that groovy is
    // not exposed as a runtime dependency.
    provided gradleApi()
    testCompile gradleApi()

    includeInJar project(path:":base:fdr", configuration:'classes')

    testCompile 'junit:junit:4.12'
    testCompile 'com.google.truth:truth:0.26'
    testCompile 'org.mockito:mockito-all:1.9.5'
    testCompile 'com.google.truth:truth:0.26'
    testCompile 'com.google.guava:guava:17.0'
    testCompile project(':base:project-test-lib')
    testCompile project(':base:testutils')
}

jar {
    into('fdr') {
        from configurations.includeInJar
    }
}

tasks.compileJava.dependsOn ":setupGradleInIde"

group = 'com.android.tools.build'
archivesBaseName = 'gradle-core'
version = rootProject.ext.buildVersion

project.ext.pomName = 'Core Library for Android Gradle Plug-in'
project.ext.pomDesc = 'Core library to build Android Gradle plugin.'

apply from: "$rootDir/buildSrc/base/publish.gradle"
apply from: "$rootDir/buildSrc/base/bintray.gradle"

test {
    environment("CUSTOM_REPO", rootProject.file("../out/repo"))

    testLogging {
        events "failed"
    }

    maxParallelForks = Runtime.runtime.availableProcessors() / 2
}

groovydoc {
    exclude     "**/internal/**"
    includePrivate false

    docTitle "Gradle Plugin for Android"
    header ""
    footer "Copyright (C) 2012 The Android Open Source Project"
    overview ""

    groovyClasspath = configurations.provided
}

task javadocJar(type: Jar, dependsOn:groovydoc) {
    classifier  'javadoc'
    from        groovydoc.destinationDir
}



// Only package JavaDoc if using --init-script=buildSrc/base/release.gradle
if (project.has("release")) {
    artifacts {
        archives javadocJar
    }
}


// Incremental update test support

Task compileIncrementalTestBaseClasses = tasks.create(
        name: "compileIncrementalTestBaseClasses",
        type: org.gradle.api.tasks.compile.JavaCompile) {
    source = new File(project.getProjectDir(), "src/test/incremental-test-classes/base")
    classpath = sourceSets.test.compileClasspath
    destinationDir = baseClasses
}

compileTestJava.dependsOn compileIncrementalTestBaseClasses

Task instrumentIncrementalTestBaseClasses = tasks.create(
        name: "instrumentIncrementalTestBaseClasses",
        type: org.gradle.api.tasks.JavaExec) {
    main = 'com.android.build.gradle.internal.incremental.IncrementalSupportVisitor'
    classpath = sourceSets.main.runtimeClasspath
    args baseClasses, baseInstrumentedClasses
}

// Compile other changesets. They have the default changset on their classpath.
Task compileIncrementalTestPatches = tasks.create("compileIncrementalTestPatches")

testClasses.dependsOn(compileIncrementalTestPatches, instrumentIncrementalTestBaseClasses)

File incrementalTestOutDir = new File(classesDir, "patches")

for (File f: new File(project.projectDir, "src/test/incremental-test-classes/patches").listFiles()) {
    System.out.println(f);
    Task t = tasks.create(
            name: "compileIncrementalTestPatch${f.getName().capitalize()}",
            type: org.gradle.api.tasks.compile.JavaCompile) {
        source = project.files(f)
        classpath = sourceSets.test.compileClasspath
        destinationDir = new File(incrementalTestOutDir, f.getName())
    }

    t.dependsOn compileIncrementalTestBaseClasses
    compileIncrementalTestPatches.dependsOn t
}


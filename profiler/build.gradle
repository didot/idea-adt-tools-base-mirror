subprojects {
  buildDir = "$rootDir/../../../out/studio/$project.name"
  repositories {
    maven { url = "$rootDir/../../../prebuilts/tools/common/m2/repository" }
  }
}

class CMakeClosureDelegate {
    String[] abis = [];
    String name;
    String projectDir;
    String[] targets = [];
    String[] flags = []

    def abis(String[] value) { abis = value; }
    def name(String value) { name = value; }
    def projectDir(String value) { projectDir = value; }
    def targets(String[] value) { targets = value}
    def flags(String[] value) { flags = value}
}

def cmake(cl) {
    CMakeClosureDelegate del = new CMakeClosureDelegate();
    cl.delegate = del;
    cl.call();
    def cmakeBuildDir = "$rootDir/../../../out/studio/${cl.owner.name}/${del.name}"
    def name = del.name;

    def cmakeTask = cl.owner.tasks.create(name: "cmake" + name.capitalize(), type: DefaultTask) {}
    def ninjaTask = cl.owner.tasks.create(name: "$name", type: DefaultTask) {}
    def preTask = cl.owner.tasks.create(name: "pre" + name.capitalize(), type: DefaultTask) {}

    del.abis.each { abi ->
        def gen = file("$cmakeBuildDir/gen/$abi");
        def extra = []
        def os = System.getProperty("os.name").toLowerCase();
        def host = os.startsWith("linux") ? "linux-x86" : "darwin-x86"

        if (!abi.equals('host')) {
            extra = [
                    "-DCMAKE_TOOLCHAIN_FILE=$rootDir/native/cmake/Android.cmake",
                    "-DPREBUILTS=$rootDir/../../../prebuilts",
                    "-DABI=$abi"
            ]
        }
        // TODO: Define different tasks for debug and release and specify
        // -DCMAKE_BUILD_TYPE=[Debug|Release]
        def cmakeAbi = cl.owner.tasks.create(name: "cmake" + name.capitalize() + abi.capitalize(), type: Exec) {
            doFirst {
                gen.deleteDir();
                gen.mkdirs();
            }
            workingDir gen
            executable "$rootDir/../../../prebuilts/cmake/$host/bin/cmake"
            args = ["-G", "Ninja", del.projectDir,
                "-DCMAKE_MAKE_PROGRAM=$rootDir/../../../prebuilts/ninja/$host/ninja",
                "-DCMAKE_RUNTIME_OUTPUT_DIRECTORY=$cmakeBuildDir/out/$abi",
                "-Dprotobuf_BUILD_TESTS=OFF",
                "-DGO_EXECUTABLE=$rootDir/../../../prebuilts/go/$host/bin/go",
                "-DCMAKE_EXPORT_COMPILE_COMMANDS=ON"] + extra + Arrays.asList(del.flags)
            environment GOROOT: "$rootDir/../../../prebuilts/go/$host"
            inputs.file "../cmake/Android.cmake"
            outputs.dir gen
        }
        cmakeTask.dependsOn cmakeAbi
        cmakeAbi.dependsOn preTask;

        def ninjaAbi = cl.owner.tasks.create(name: "ninja" + name.capitalize() + abi.capitalize(), type: Exec) {
            workingDir gen
            args = Arrays.asList(del.targets)

            executable "$rootDir/../../../prebuilts/ninja/$host/ninja"
            environment GOROOT: "$rootDir/../../../prebuilts/go/$host"
            inputs.dir del.projectDir
            inputs.dir gen
            outputs.dir "$cmakeBuildDir/out/$abi"
        }
        ninjaAbi.dependsOn cmakeAbi
        ninjaTask.dependsOn ninjaAbi
    }
    return ninjaTask
}

